*** Settings ***
Test Setup       Go To   ${ROOT}forms/named_submit_buttons.html
Default Tags     submit
Resource         ../resource.txt
Library          OperatingSystem

*** Test Cases ***
Submit Form
    [Documentation]   LOG 1 Submitting form 'form_name'.
    Submit Form   form_name
    Location Should Be   ${ROOT}forms/submit.html

Submit Form Without Args
    [Setup]   Go To   ${ROOT}forms/form_without_name.html
    Submit Form
    Location Should Be   ${ROOT}target/first.html

Click Ok Button By Name
    [Documentation]   LOG 1 Clicking button 'ok_button'.
    Click Button   ok_button
    Location Should Be   ${ROOT}forms/submit.html

Click Cancel Button By Name
    Click Button   cancel_button   And Don't Wait
    Value Should Be Cancel

Click Ok Button By Value
    Click Button   Ok
    Location Should Be   ${ROOT}forms/submit.html

Click Cancel Button By Value
    Click Button   cancel_button   And Don't Wait
    Value Should Be Cancel

Form With Button (Not Input) Tag
    [Setup]   Go To   ${ROOT}forms/buttons.html
    Click Button   button
    Location Should Be   ${ROOT}forms/submit.html

Choose File
    [Setup]   Navigate To File Upload Form And Create Temp File To Upload
    Choose File   file_to_upload   ${CURDIR}${/}temp.txt
    Text Field Value Should Be   file_to_upload   ${CURDIR}${/}temp.txt
    [Teardown]  Remove File   ${CURDIR}${/}temp.txt

Click Image With Submit Type Images
    [Setup]   Go To   ${ROOT}forms/form_with_image_submit.html
    Click Image   robot.bmp
    Location Should Be   ${ROOT}forms/submit.html


*** Keywords ***
Value Should Be Cancel
    ${value} =   Get Value   textfield
    Equals   ${value}   Cancel

Navigate To File Upload Form And CreateTemp File To Upload
  Cannot Be Executed in IE
  Go To   ${ROOT}forms/file_upload_form.html
  Touch   ${CURDIR}/temp.txt

